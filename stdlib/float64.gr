/**
 * @module Float64: Operations on the Float64 type.
 * @example import Float64 from "float64"
 */
import WasmI32 from "runtime/unsafe/wasmi32"
import WasmF64 from "runtime/unsafe/wasmf64"
import Memory from "runtime/unsafe/memory"
import {
  newFloat64
} from "runtime/dataStructures"

import {
  coerceNumberToFloat64 as fromNumber,
  coerceFloat64ToNumber as toNumber
} from "runtime/numbers"

/**
 * @section Values: Functions for working with the `Float64` data type.
 */

/**
 * Coverts the argument from a `Number` to a `Float64`.
 *
 * @param x: The `Number` to convert
 * @returns The converted `Number`
 */
export fromNumber

/**
 * Coverts the argument from a `Float64` to a `Number`.
 *
 * @param x: The `Float64` to convert
 * @returns The converted `Float64`
 */
export toNumber

/**
 * Floating-point addition.
 *
 * @param x: The first operand
 * @param y: The second operand
 * @returns The sum of the two operands
 */
@disableGC
export let rec add = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ptr = newFloat64(WasmF64.add(xv, yv))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(add))
  ret
}

/**
 * Floating-point subtraction.
 *
 * @param x: The first operand
 * @param y: The second operand
 * @returns The difference of the two operands
 */
@disableGC
export let rec sub = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ptr = newFloat64(WasmF64.sub(xv, yv))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(sub))
  ret
}

/**
 * Floating-point multiplication.
 *
 * @param x: The first operand
 * @param y: The second operand
 * @returns The product of the two operands
 */
@disableGC
export let rec mul = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ptr = newFloat64(WasmF64.mul(xv, yv))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(mul))
  ret
}

/**
 * Floating-point division.
 *
 * @param x: The first operand
 * @param y: The second operand
 * @returns The quotient of the two operands
 */
@disableGC
export let rec div = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ptr = newFloat64(WasmF64.div(xv, yv))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(div))
  ret
}

/**
 * @section Comparisons: Comparison functions for working with floating points.
 */

/**
 * Checks if the first argument is strictly less than the second argument.
 *
 * @param x: The first argument
 * @param y: The second argument
 * @returns A boolean representing if the first argument is less then the second argument
 */
@disableGC
export let rec lt = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ret = WasmF64.lt(xv, yv)
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(lt))
  ret
}

/**
 * Checks if the first argument is strictly greater than the second argument.
 *
 * @param x: The first argument
 * @param y: The second argument
 * @returns A boolean representing if the first argument is greater then the second argument
 */
@disableGC
export let rec gt = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ret = WasmF64.gt(xv, yv)
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(gt))
  ret
}

/**
 * Checks if the first argument is less than or equal to the second argument.
 *
 * @param x: The first argument
 * @param y: The second argument
 * @returns A boolean representing if the first argument is less then or equal to the second argument
 */
@disableGC
export let rec lte = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ret = WasmF64.le(xv, yv)
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(lte))
  ret
}

/**
 * Checks if the first argument is greater than or equal to the second argument.
 *
 * @param x: The first argument
 * @param y: The second argument
 * @returns A boolean representing if the first argument is greater then or equal to the second argument
 */
@disableGC
export let rec gte = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ret = WasmF64.ge(xv, yv)
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(gte))
  ret
}

/**
 * @section Constants: Constants related to floating points.
 */

/**
 * Generate infinity as a `Float64`.
 *
 * @returns Infinity as a `Float64`
 */
@disableGC
let rec makeInfinity = () => {
  let ptr = newFloat64(WasmF64.reinterpretI64(0b0111111111110000000000000000000000000000000000000000000000000000N))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(makeInfinity))
  ret
}

/**
 * Infinity as a `Float64`.
 */
export let infinity = makeInfinity()

/**
 * Generate Nan as a `Float64`.
 *
 * @returns Nan as a `Float64`
 */
@disableGC
let rec makeNaN = () => {
  let ptr = newFloat64(WasmF64.reinterpretI64(0b0111111111110000000000000000000000000000000000000000000000000001N))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(makeNaN))
  ret
}

/**
 * Nan as a `Float64`.
 */
export let nan = makeNaN()
