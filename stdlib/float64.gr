/**
 * @module Float64: Utilities for working with the Float64 type.
 * @example import Float64 from "float64"
 * 
 * @since v0.2.0
 */
import WasmI32 from "runtime/unsafe/wasmi32"
import WasmF64 from "runtime/unsafe/wasmf64"
import Memory from "runtime/unsafe/memory"
import {
  newFloat64
} from "runtime/dataStructures"

import {
  coerceNumberToFloat64 as fromNumber,
  coerceFloat64ToNumber as toNumber
} from "runtime/numbers"

/**
 * @section Conversions: Functions for converting between Numbers and the Float64 type.
 */

/**
 * Converts a Number to a Float64.
 *
 * @param number: The value to convert
 * @returns The Number represented as a Float64
 * 
 * @since v0.2.0
 */
export fromNumber

/**
 * Converts a Float64 to a Number.
 *
 * @param float: The value to convert
 * @returns The Float64 represented as a Number
 * 
 * @since v0.2.0
 */
export toNumber

/**
 * @section Operations: Mathematical operations for Float64 values.
 */

/**
 * Computes the sum of its operands.
 *
 * @param x: The first operand
 * @param y: The second operand
 * @returns The sum of the two operands
 * 
 * @since v0.2.0
 */
@disableGC
export let rec add = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ptr = newFloat64(WasmF64.add(xv, yv))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(add))
  ret
}

/**
 * Computes the difference of its operands.
 *
 * @param x: The first operand
 * @param y: The second operand
 * @returns The difference of the two operands
 * 
 * @since v0.2.0
 */
@disableGC
export let rec sub = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ptr = newFloat64(WasmF64.sub(xv, yv))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(sub))
  ret
}

/**
 * Computes the product of its operands.
 *
 * @param x: The first operand
 * @param y: The second operand
 * @returns The product of the two operands
 * 
 * @since v0.2.0
 */
@disableGC
export let rec mul = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ptr = newFloat64(WasmF64.mul(xv, yv))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(mul))
  ret
}

/**
 * Computes the quotient of its operands.
 *
 * @param x: The first operand
 * @param y: The second operand
 * @returns The quotient of the two operands
 * 
 * @since v0.2.0
 */
@disableGC
export let rec div = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ptr = newFloat64(WasmF64.div(xv, yv))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(div))
  ret
}

/**
 * @section Comparisons: Functions for comparing Float64 values.
 */

/**
 * Checks if the first value is less than the second value.
 *
 * @param x: The first value
 * @param y: The second value
 * @returns `true` if the first value is less than the second value or `false` otherwise
 * 
 * @since v0.2.0
 */
@disableGC
export let rec lt = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ret = WasmF64.lt(xv, yv)
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(lt))
  ret
}

/**
 * Checks if the first value is greater than the second value.
 *
 * @param x: The first value
 * @param y: The second value
 * @returns `true` if the first value is greater than the second value or `false` otherwise
 * 
 * @since v0.2.0
 */
@disableGC
export let rec gt = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ret = WasmF64.gt(xv, yv)
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(gt))
  ret
}

/**
 * Checks if the first value is less than or equal to the second value.
 *
 * @param x: The first value
 * @param y: The second value
 * @returns `true` if the first value is less than or equal to the second value or `false` otherwise
 * 
 * @since v0.2.0
 */
@disableGC
export let rec lte = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ret = WasmF64.le(xv, yv)
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(lte))
  ret
}

/**
 * Checks if the first value is greater than or equal to the second value.
 *
 * @param x: The first value
 * @param y: The second value
 * @returns `true` if the first value is greater than or equal to the second value or `false` otherwise
 * 
 * @since v0.2.0
 */
@disableGC
export let rec gte = (x: Float64, y: Float64) => {
  let xv = WasmF64.load(WasmI32.fromGrain(x), 8n)
  let yv = WasmF64.load(WasmI32.fromGrain(y), 8n)
  let ret = WasmF64.ge(xv, yv)
  Memory.decRef(WasmI32.fromGrain(x))
  Memory.decRef(WasmI32.fromGrain(y))
  Memory.decRef(WasmI32.fromGrain(gte))
  ret
}

/**
 * @section Constants: Float64 constant values.
 */

@disableGC
let rec makeInfinity = () => {
  let ptr = newFloat64(WasmF64.reinterpretI64(0b0111111111110000000000000000000000000000000000000000000000000000N))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(makeInfinity))
  ret
}

/**
 * Infinity represented as a Float64 value.
 * 
 * @since v0.4.0
 */
export let infinity = makeInfinity()

@disableGC
let rec makeNaN = () => {
  let ptr = newFloat64(WasmF64.reinterpretI64(0b0111111111110000000000000000000000000000000000000000000000000001N))
  let ret = WasmI32.toGrain(ptr) : Float64
  Memory.decRef(WasmI32.fromGrain(makeNaN))
  ret
}

/**
 * NaN (Not a Number) represented as a Float64 value.
 * 
 * @since v0.4.0
 */
export let nan = makeNaN()
