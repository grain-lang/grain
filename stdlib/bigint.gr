/**
 * Utilities for working with the BigInt type.
 *
 * @example from "bigint" include Bigint
 *
 * @example 9223372036854775809t
 * @example 0t
 * @example -9223372036854775809t
 *
 * @since v0.5.0
 */
module BigInt

from "runtime/unsafe/wasmi32" include WasmI32
from "runtime/unsafe/memory" include Memory
from "runtime/bigint" include Bigint as BI
from "runtime/dataStructures" include DataStructures as DS

from "runtime/numbers" include Numbers
use Numbers.{
  coerceNumberToBigInt as fromNumber,
  coerceBigIntToNumber as toNumber,
}

provide { fromNumber, toNumber }

/**
 * Increments the value by one.
 *
 * @param num: The value to increment
 * @returns The incremented value
 *
 * @example BigInt.incr(1t) == 2t
 * @example BigInt.incr(-2t) == -1t
 *
 * @since v0.5.0
 */
@unsafe
provide let incr = (num: BigInt) => {
  WasmI32.toGrain(BI.incr(WasmI32.fromGrain(num))): BigInt
}

/**
 * Decrements the value by one.
 *
 * @param num: The value to decrement
 * @returns The decremented value
 *
 * @example BigInt.decr(2t) == 1t
 * @example BigInt.decr(-2t) == -3t
 *
 * @since v0.5.0
 */
@unsafe
provide let decr = (num: BigInt) => {
  WasmI32.toGrain(BI.decr(WasmI32.fromGrain(num))): BigInt
}

/**
 * Negates the given operand.
 *
 * @param num: The operand
 * @returns The negated operand
 *
 * @example BigInt.neg(1t) == -1t
 * @example BigInt.neg(-1t) == 1t
 *
 * @since v0.5.0
 */
@unsafe
provide let neg = (num: BigInt) => {
  WasmI32.toGrain(BI.negate(WasmI32.fromGrain(num))): BigInt
}

/**
 * Returns the absolute value of the given operand.
 *
 * @param num: The operand
 * @returns The operand's absolute value
 *
 * @example BigInt.abs(1t) == 1t
 * @example BigInt.abs(-1t) == 1t
 *
 * @since v0.5.0
 */
@unsafe
provide let abs = (num: BigInt) => {
  WasmI32.toGrain(BI.abs(WasmI32.fromGrain(num))): BigInt
}

/**
 * Computes the sum of its operands.
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns The sum of the two operands
 *
 * @example
 * use BigInt.{ (+) }
 * assert 1t + 1t == 2t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `add`
 */
@unsafe
provide let (+) = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(BI.add(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))):
    BigInt
}

/**
 * Computes the difference of its operands.
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns The difference of the two operands
 *
 * @example
 * use BigInt.{ (-) }
 * assert 3t - 1t == 2t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `sub`
 */
@unsafe
provide let (-) = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(BI.sub(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))):
    BigInt
}

/**
 * Computes the product of its operands.
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns The product of the two operands
 *
 * @example
 * use BigInt.{ (*) }
 * assert 3t * 3t == 9t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `mul`
 */
@unsafe
provide let (*) = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(BI.mul(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))):
    BigInt
}

// For further reading on Truncated vs. Floored division: https://en.wikipedia.org/wiki/Modulo_operation

/**
 * Computes the quotient of its operands using signed (truncated) division
 * (in which the quotient is always rounded towards zero).
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns The quotient of its operands
 *
 * @example
 * use BigInt.{ (/) }
 * assert 9t / 3t == 3t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `div`
 */
@unsafe
provide let (/) = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(BI.quot(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))):
    BigInt
}

/**
 * Computes the remainder of the division of its operands using signed (truncated) division
 * (in which the quotient is always rounded towards zero).
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns The remainder of its operands
 *
 * @example BigInt.rem(3t, 2t) == 1t
 *
 * @since v0.5.0
 */
@unsafe
provide let rem = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(BI.rem(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))):
    BigInt
}

/**
 * Computes the quotient and remainder of its operands using signed (truncated) division.
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns The quotient and remainder of its operands
 *
 * @example BigInt.quotRem(7t, 2t) == (3t, 1t))
 *
 * @since v0.5.0
 */
@unsafe
provide let quotRem = (num1: BigInt, num2: BigInt) => {
  let dest = Memory.malloc(8n)
  BI.quotRem(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2), dest)
  let q = WasmI32.toGrain(WasmI32.load(dest, 0n)): BigInt
  let r = WasmI32.toGrain(WasmI32.load(dest, 4n)): BigInt
  Memory.free(dest)
  (q, r)
}

/**
 * Computes the greatest common divisior of the two operands.
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns The greatest common divisor of its operands
 *
 * @example BigInt.gcd(36t, 24t) == 12t
 *
 * @since v0.5.0
 */
@unsafe
provide let gcd = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(BI.gcd(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))):
    BigInt
}

/**
 * Shifts the bits of the value left by the given number of bits.
 *
 * @param num: The value to shift
 * @param places: The number of bits to shift by
 * @returns The shifted value
 *
 * @example
 * use BigInt.{ (<<) }
 * assert (10t << 2l) == 40t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `shl`
 */
@unsafe
provide let (<<) = (num: BigInt, places: Int32) => {
  let num = WasmI32.fromGrain(num)
  let places = WasmI32.load(WasmI32.fromGrain(places), 4n)
  WasmI32.toGrain(BI.shl(num, places)): BigInt
}

/**
 * Shifts the bits of the value right by the given number of bits, preserving the sign bit.
 *
 * @param num: The value to shift
 * @param places: The amount to shift by
 * @returns The shifted value
 *
 * @example
 * use BigInt.{ (>>) }
 * assert (9999t >> 2l) == 2499t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `shr`
 */
@unsafe
provide let (>>) = (num: BigInt, places: Int32) => {
  let num = WasmI32.fromGrain(num)
  let places = WasmI32.load(WasmI32.fromGrain(places), 4n)
  WasmI32.toGrain(BI.shrS(num, places)): BigInt
}

/**
 * Checks if the given value is equal to zero.
 *
 * @param num: The value to inspect
 * @returns `true` if the first value is equal to zero or `false` otherwise
 *
 * @example assert BigInt.eqz(0t) == true
 * @example assert BigInt.eqz(1t) == false
 *
 * @since v0.5.0
 */
@unsafe
provide let eqz = (num: BigInt) => {
  let result = BI.eqz(WasmI32.fromGrain(num))
  ignore(num)
  result
}

/**
 * Checks if the first value is equal to the second value.
 *
 * @param num1: The first value
 * @param num2: The second value
 * @returns `true` if the first value is equal to the second value or `false` otherwise
 *
 * @example
 * use BigInt.{ (==) }
 * assert 1t == 1t
 * @example
 * use BigInt.{ (==) }
 * assert -10t == -10t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `eq`
 */
@unsafe
provide let (==) = (num1: BigInt, num2: BigInt) => {
  let result = BI.eq(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ignore(num1)
  ignore(num2)
  result
}

/**
 * Checks if the first value is not equal to the second value.
 *
 * @param num1: The first value
 * @param num2: The second value
 * @returns `true` if the first value is not equal to the second value or `false` otherwise
 *
 * @example
 * use BigInt.{ (!=) }
 * assert 1t != 2t
 * @example
 * use BigInt.{ (!=) }
 * assert -10t != -20t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `ne`
 */
@unsafe
provide let (!=) = (num1: BigInt, num2: BigInt) => {
  let result = BI.ne(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ignore(num1)
  ignore(num2)
  result
}

/**
 * Checks if the first value is less than the second value.
 *
 * @param num1: The first value
 * @param num2: The second value
 * @returns `true` if the first value is less than the second value or `false` otherwise
 *
 * @example
 * use BigInt.{ (<) }
 * assert 1t < 2t
 * @example
 * use BigInt.{ (<) }
 * assert -10t < 0t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `lt`
 */
@unsafe
provide let (<) = (num1: BigInt, num2: BigInt) => {
  let result = BI.lt(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ignore(num1)
  ignore(num2)
  result
}

/**
 * Checks if the first value is less than or equal to the second value.
 *
 * @param num1: The first value
 * @param num2: The second value
 * @returns `true` if the first value is less than or equal to the second value or `false` otherwise
 *
 * @example
 * use BigInt.{ (<=) }
 * assert 1t <= 1t
 * @example
 * use BigInt.{ (<=) }
 * assert -10t <= 0t
 * @example
 * use BigInt.{ (<=) }
 * assert 2t <= 3t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `lte`
 */
@unsafe
provide let (<=) = (num1: BigInt, num2: BigInt) => {
  let result = BI.lte(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ignore(num1)
  ignore(num2)
  result
}

/**
 * Checks if the first value is greater than the second value.
 *
 * @param num1: The first value
 * @param num2: The second value
 * @returns `true` if the first value is greater than the second value or `false` otherwise
 *
 * @example
 * use BigInt.{ (>) }
 * assert 2t > 1t
 * @example
 * use BigInt.{ (>) }
 * assert 0t > -10t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `gt`
 */
@unsafe
provide let (>) = (num1: BigInt, num2: BigInt) => {
  let result = BI.gt(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ignore(num1)
  ignore(num2)
  result
}

/**
 * Checks if the first value is greater than or equal to the second value.
 *
 * @param num1: The first value
 * @param num2: The second value
 * @returns `true` if the first value is greater than or equal to the second value or `false` otherwise
 *
 * @example
 * use BigInt.{ (>=) }
 * assert 1t >= 1t
 * @example
 * use BigInt.{ (>=) }
 * assert 0t >= -10t
 * @example
 * use BigInt.{ (>=) }
 * assert 3t >= 2t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `gte`
 */
@unsafe
provide let (>=) = (num1: BigInt, num2: BigInt) => {
  let result = BI.gte(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ignore(num1)
  ignore(num2)
  result
}

/**
 * Computes the bitwise NOT of the given value.
 *
 * @param num: The given value
 * @returns Containing the inverted bits of the given value
 *
 * @example BigInt.lnot(91234t) == -91235t
 *
 * @since v0.5.0
 */
@unsafe
provide let lnot = (num: BigInt) => {
  WasmI32.toGrain(BI.bitwiseNot(WasmI32.fromGrain(num))): BigInt
}

/**
 * Computes the bitwise AND (`&`) on the given operands.
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns Containing a `1` in each bit position for which the corresponding bits of both operands are `1`
 *
 * @example
 * use BigInt.{ (&) }
 * assert (4t & 3t) == 0t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `land`
 */
@unsafe
provide let (&) = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(
    BI.bitwiseAnd(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ): BigInt
}

/**
 * Computes the bitwise OR (`|`) on the given operands.
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns Containing a `1` in each bit position for which the corresponding bits of either or both operands are `1`
 *
 * @example
 * use BigInt.{ (|) }
 * assert (5t | 3t) == 7t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `lor`
 */
@unsafe
provide let (|) = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(
    BI.bitwiseOr(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ): BigInt
}

/**
 * Computes the bitwise XOR (`^`) on the given operands.
 *
 * @param num1: The first operand
 * @param num2: The second operand
 * @returns Containing a `1` in each bit position for which the corresponding bits of either but not both operands are `1`
 *
 * @example
 * use BigInt.{ (^) }
 * assert (5t ^ 3t) == 6t
 *
 * @since v0.6.0
 * @history v0.5.0: Originally named `lxor`
 */
@unsafe
provide let (^) = (num1: BigInt, num2: BigInt) => {
  WasmI32.toGrain(
    BI.bitwiseXor(WasmI32.fromGrain(num1), WasmI32.fromGrain(num2))
  ): BigInt
}

/**
 * Counts the number of leading zero bits in the value.
 * Will return the maximum integer for negative numbers.
 *
 * @param num: The value to inspect
 * @returns The amount of leading zeros
 *
 * @example BigInt.clz(5t) == 2147483647t
 *
 * @since v0.5.0
 */
@unsafe
provide let clz = (num: BigInt) => {
  WasmI32.toGrain(DS.newInt32(BI.countLeadingZeros(WasmI32.fromGrain(num)))):
    Int32
}

/**
 * Counts the number of trailing zero bits in the value.
 *
 * @param num: The value to inspect
 * @returns The amount of trailing zeros
 *
 * @example BigInt.ctz(14t) == 1t
 *
 * @since v0.5.0
 */
@unsafe
provide let ctz = (num: BigInt) => {
  WasmI32.toGrain(DS.newInt64(BI.countTrailingZeros(WasmI32.fromGrain(num)))):
    Int64
}

/**
 * Counts the number of bits set to `1` in the value, also known as a population count.
 * Will return the `None` if given a negative integer
 *
 * @param num: The value to inspect
 * @returns The amount of 1-bits in its operand
 *
 * @example BigInt.popcnt(14t) == 1t
 *
 * @since v0.5.0
 */
@unsafe
provide let popcnt = (num: BigInt) => {
  let flagDest = Memory.malloc(4n)
  WasmI32.store(flagDest, 0n, 0n)
  let res = BI.popcnt(WasmI32.fromGrain(num), flagDest)
  if (WasmI32.eqz(WasmI32.load(flagDest, 0n))) {
    Some(WasmI32.toGrain(DS.newInt64(res)): Int64)
  } else {
    None
  }
}

/**
 * Converts the given operand to a string.
 *
 * @param num: The operand
 * @returns The operand, as a string
 *
 * @example BigInt.toString(1t) == "1"
 * @example BigInt.toString(-1t) == "-1"
 *
 * @since v0.5.0
 */
@unsafe
provide let toString = (num: BigInt) => {
  let string = BI.bigIntToString10(WasmI32.fromGrain(num))
  ignore(num)
  string
}
