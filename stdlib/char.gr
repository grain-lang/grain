/**
 * @module Char: Utilities for working with the Char type.
 *
 * The Char type represents a single [Unicode scalar value](https://www.unicode.org/glossary/#unicode_scalar_value).
 *
 * @example import Char from "char"
 * 
 * @since 0.3.0
 */

import WasmI32 from "runtime/unsafe/wasmi32"
import Memory from "runtime/unsafe/memory"
import Errors from "runtime/unsafe/errors"
import {
  tagSimpleNumber,
  allocateChar,
  allocateString,
} from "runtime/dataStructures"

exception MalformedUtf8

/**
 * @section Values: Functions and constants included in the Char module.
 */

/**
 * The minimum valid Unicode scalar value.
 * 
 * @since 0.3.0
 */
export let min = 0x0000
/**
 * The maximum valid Unicode scalar value.
 * 
 * @since 0.3.0
 */
export let max = 0x10FFFF

/**
 * Determines whether the given character code is a valid Unicode scalar value.
 * 
 * @param charCode: The number to check
 * @returns `true` if the number refers to a valid Unicode scalar value or `false` otherwise
 * 
 * @since 0.3.0
 */
export let isValid = charCode => {
  charCode >= min &&
  (charCode <= 0xD7FF || charCode >= 0xE000) &&
  charCode <= max
}

/**
 * Determines the Unicode scalar value for a character.
 * 
 * @param char: The character
 * @returns The Unicode scalar value for the given character
 * 
 * @since 0.3.0
 */
@disableGC
export let rec code = (char: Char) => {
  // Algorithm from https://encoding.spec.whatwg.org/#utf-8-decoder

  let char = WasmI32.fromGrain(char)

  let (+) = WasmI32.add
  let (==) = WasmI32.eq
  let (>=) = WasmI32.geU
  let (<=) = WasmI32.leU
  let (<<) = WasmI32.shl
  let (&) = WasmI32.and
  let (|) = WasmI32.or

  let mut codePoint = 0n
  let mut bytesSeen = 0n
  let mut bytesNeeded = 0n
  let mut lowerBoundary = 0x80n
  let mut upperBoundary = 0xBFn

  let mut offset = 0n

  let mut result = 0n

  while (true) {
    let byte = WasmI32.load8U(char + offset, 4n)
    offset += 1n
    if (bytesNeeded == 0n) {
      if (byte >= 0x00n && byte <= 0x7Fn) {
        result = byte
        break
      } else if (byte >= 0xC2n && byte <= 0xDFn) {
        bytesNeeded = 1n
        codePoint = byte & 0x1Fn
      } else if (byte >= 0xE0n && byte <= 0xEFn) {
        if (byte == 0xE0n) lowerBoundary = 0xA0n
        if (byte == 0xEDn) upperBoundary = 0x9Fn
        bytesNeeded = 2n
        codePoint = byte & 0xFn
      } else if (byte >= 0xF0n && byte <= 0xF4n) {
        if (byte == 0xF0n) lowerBoundary = 0x90n
        if (byte == 0xF4n) upperBoundary = 0x8Fn
        bytesNeeded = 3n
        codePoint = byte & 0x7n
      } else {
        throw MalformedUtf8
      }
      continue
    }
    if (!(lowerBoundary <= byte && byte <= upperBoundary)) {
      throw MalformedUtf8
    }
    lowerBoundary = 0x80n
    upperBoundary = 0xBFn
    codePoint = codePoint << 6n | byte & 0x3Fn
    bytesSeen += 1n
    if (bytesSeen == bytesNeeded) {
      result = codePoint
      break
    }
  }

  Memory.decRef(char)
  Memory.decRef(WasmI32.fromGrain(code))
  tagSimpleNumber(result)
}

/**
 * Creates a character from the given Unicode scalar value.
 * Throws an exception if the Unicode scalar value is invalid.
 * 
 * @param usv: The Unicode scalar value
 * @returns The character for the given Unicode scalar value
 * 
 * @since 0.3.0
 */
@disableGC
export let rec fromCode = (usv: Number) => {
  // Algorithm from https://encoding.spec.whatwg.org/#utf-8-encoder

  let (+) = WasmI32.add
  let (-) = WasmI32.sub
  let (*) = WasmI32.mul
  let (==) = WasmI32.eq
  let (>) = WasmI32.gtU
  let (<=) = WasmI32.leU
  let (<) = WasmI32.ltU
  let (>>>) = WasmI32.shrU
  let (&) = WasmI32.and
  let (|) = WasmI32.or

  let usv = WasmI32.fromGrain(usv)
  if ((usv & 1n) == 0n) {
    throw InvalidArgument("Invalid character code")
  }

  let usv = usv >>> 1n
  let result = if (usv < 0x80n) {
    let char = allocateChar()
    WasmI32.store8(char, usv, 4n)
    WasmI32.toGrain(char): Char
  } else {
    let mut count = 0n
    let mut offset = 0n
    if (usv <= 0x07FFn) {
      count = 1n
      offset = 0xC0n
    } else if (usv <= 0xFFFFn) {
      count = 2n
      offset = 0xE0n
    } else {
      count = 3n
      offset = 0xF0n
    }
    let char = allocateChar()
    WasmI32.store8(char, (usv >>> 6n * count) + offset, 4n)

    let mut n = 0n
    while (count > 0n) {
      n += 1n
      let temp = usv >>> 6n * (count - 1n)
      WasmI32.store8(char + n, 0x80n | temp & 0x3Fn, 4n)
      count -= 1n
    }

    WasmI32.toGrain(char): Char
  }

  // We've asserted that the original `code` was a stack allocated number so
  // no need to decRef it
  Memory.decRef(WasmI32.fromGrain(fromCode))
  result
}

/**
 * Returns the next valid character by Unicode scalar value.
 * Throws if the input character is the max valid Unicode scalar value.
 * 
 * @param char: The character
 * @returns The next valid character by Unicode scalar value
 * 
 * @since 0.3.0
 */
export let succ = char => {
  let codePoint = code(char)
  if (codePoint == max) {
    fail "no valid Unicode scalar value past U+10FFF"
  } else if (codePoint == 0xD7FF) {
    fromCode(0xE000)
  } else {
    fromCode(codePoint + 1)
  }
}

/**
 * Returns the previous valid character by Unicode scalar value.
 * Throws if the input character is the min valid Unicode scalar value.
 * 
 * @param char: The character
 * @returns The previous valid character by Unicode scalar value
 * 
 * @since 0.3.0
 */
export let pred = char => {
  let codePoint = code(char)
  if (codePoint == min) {
    fail "no valid Unicode scalar value below U+0000"
  } else if (codePoint == 0xE000) {
    fromCode(0xD7FF)
  } else {
    fromCode(codePoint - 1)
  }
}

/**
 * Converts the given character to a string.
 * 
 * @param char: The character to convert
 * @returns A string containing the given character
 * 
 * @since 0.3.0
 */
@disableGC
export let rec toString = (char: Char) => {
  let (+) = WasmI32.add
  let (&) = WasmI32.and
  let (==) = WasmI32.eq

  let char = WasmI32.fromGrain(char)
  let byte = WasmI32.load8U(char, 4n)
  let n = if ((byte & 0x80n) == 0x00n) {
    1n
  } else if ((byte & 0xF0n) == 0xF0n) {
    4n
  } else if ((byte & 0xE0n) == 0xE0n) {
    3n
  } else {
    2n
  }
  let str = allocateString(n)
  Memory.copy(str + 8n, char + 4n, n)
  let ret = WasmI32.toGrain(str): String
  Memory.decRef(WasmI32.fromGrain(char))
  Memory.decRef(WasmI32.fromGrain(toString))
  ret
}
