module DebugPrint

include "runtime/numberUtils" as Utils
include "runtime/unsafe/wasmi32"
from WasmI32 use { (+) }
include "runtime/unsafe/wasmf64"
include "runtime/unsafe/memory"

foreign wasm fd_write: (
  WasmI32,
  WasmI32,
  WasmI32,
  WasmI32,
) => WasmI32 from "wasi_snapshot_preview1"

@unsafe
provide let print = (s: String) => {
  let ptr = WasmI32.fromGrain(s)
  // iov: [<ptr to string> <nbytes of string> <ptr to newline> <nbytes of newline>] (32 bytes)
  // buf: <iov> <written> <newline char>
  // fd_write(STDOUT (1), iov, len(iov), written)
  let buf = Memory.malloc(37n)
  let iov = buf
  let written = buf + 32n
  let lf = buf + 36n
  WasmI32.store(iov, ptr + 8n, 0n)
  WasmI32.store(iov, WasmI32.load(ptr, 4n), 4n)
  WasmI32.store8(lf, 10n, 0n)
  WasmI32.store(iov, lf, 8n)
  WasmI32.store(iov, 1n, 12n)
  fd_write(1n, iov, 2n, written)
  Memory.free(buf)
  void
}

@unsafe
provide let printI32 = val => {
  print(Utils.itoa32(val, 10n))
}

@unsafe
provide let printI64 = val => {
  print(Utils.itoa64(val, 10n))
}

@unsafe
provide let printF32 = val => {
  print(Utils.dtoa(WasmF64.promoteF32(val)))
}

@unsafe
provide let printF64 = val => {
  print(Utils.dtoa(val))
}
