/* grainc-flags --compilation-mode=runtime */

/*
 * This module implements a generic memory allocator.
 * The algorithm is quite simple, being based on the memory allocator
 * from pages 185-188 of K&R C (2nd edition).
 */

import WasmI32, {
  add as (+),
  sub as (-),
  mul as (*),
  divU as (/),
  gtU as (>),
  geU as (>=),
  ltU as (<),
  shl as (<<),
  shrU as (>>),
  eq as (==),
  ne as (!=),
} from "runtime/unsafe/wasmi32"
import Exception from "runtime/exception"

primitive memorySize: () -> WasmI32 = "@wasm.memory_size"
primitive memoryGrow: WasmI32 -> WasmI32 = "@wasm.memory_grow"

primitive (!): Bool -> Bool = "@not"
primitive (&&): (Bool, Bool) -> Bool = "@and"
primitive (||): (Bool, Bool) -> Bool = "@or"

primitive heapBase: WasmI32 = "@heap.base"

/* UNDERSTANDING THE STRUCTURE OF THE FREE LIST
 * The original K&R definition for the free list entry type was the following:
 *
 *     union header {
 *         struct {
 *             union header *ptr;
 *             unsigned size;
 *         } s;
 *         long x; // <- forces 8-byte alignment
 *     };
 *
 * In memory, this is really just two ints (assuming we're working in 32-bit mode).
 * As such, we manually lay out the entries on the heap as follows (note that we
 * use helpers to facilitate accessing and setting these values):
 *
 *   Grain             C Equivalent
 *   ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
 *   let ptr      ===  union header *ptr
 *   getNext(ptr) ===  ptr->s.ptr
 *   getSize(ptr) ===  ptr->s.size
 */

/**
 * Pointer to the start of the free list. This is always a multiple of
 * 8, with the exception of its initial value (used as a sentinel).
 */
let mut freePtr = 1n

/**
 * Size (in bytes) of entries in the free list.
 */
let mallocHeaderSize = 8n

/**
 * log_2(mallocHeaderSize) (multiplication by the header
 * size is equivalent to left-shifting by this amount)
 */
let logMallocHeaderSize = 3n

/**
 * The current size (in bytes) of the heap.
 */
let mut heapSize = 0n

export let _RESERVED_RUNTIME_SPACE = 0x4000n

/**
 * The base the heap. The block at this address will be size 0 and
 * serve as the root of the free list.
 */
let _BASE = heapBase + _RESERVED_RUNTIME_SPACE

/**
 * The start pointer of the heap.
 */
let _HEAP_START = _BASE + mallocHeaderSize

let _NEXT_OFFSET = 0n
let _SIZE_OFFSET = 4n

/**
 * Size (in bytes) of a single WebAssembly page
 */
let _PAGE_SIZE = 65536n

let getNext = (ptr: WasmI32) => {
  WasmI32.load(ptr, _NEXT_OFFSET)
}

let setNext = (ptr: WasmI32, val: WasmI32) => {
  WasmI32.store(ptr, val, _NEXT_OFFSET)
}

let getSize = (ptr: WasmI32) => {
  WasmI32.load(ptr, _SIZE_OFFSET)
}

let setSize = (ptr: WasmI32, val: WasmI32) => {
  WasmI32.store(ptr, val, _SIZE_OFFSET)
}

/**
 * Requests that the heap be grown by the given number of bytes.
 *
 * @param nbytes: The number of bytes requested
 * @returns The pointer to the beginning of the extended region if successful or -1 otherwise
 */
let growHeap = (nbytes: WasmI32) => {
  let mut reqSize = 0n
  let mut reqResult = 0n
  let mut origSize = heapSize

  // If the size has not been initialized, do so.
  if (heapSize == 0n) {
    heapSize = memorySize() * _PAGE_SIZE - _HEAP_START
    if (nbytes > heapSize) {
      // More bytes requested than the initial heap size,
      // so we need to request more anyway.
      reqSize = nbytes - heapSize
      reqSize = reqSize >> 16n
      reqSize += 1n
      reqResult = memoryGrow(reqSize)
      if (reqResult == -1n) {
        -1n
      } else {
        heapSize += reqSize << 16n
        _HEAP_START
      }
    } else {
      _HEAP_START
    }
  } else {
    // The size has already been initialized, so call the external function.
    let newRegionStart = memorySize() * _PAGE_SIZE

    reqSize = nbytes
    reqSize = reqSize >> 16n
    reqSize += 1n

    reqResult = memoryGrow(reqSize)
    if (reqResult == -1n) {
      -1n
    } else {
      heapSize += reqSize << 16n
      newRegionStart
    }
  }
}

/**
 * Frees the given allocated pointer.
 *
 * @param ap: The pointer to free
 */
export let free = (ap: WasmI32) => {
  let mut blockPtr = ap - 8n // 8 bytes for malloc header
  let mut p = freePtr

  // Edge case: for the first free (called by morecore), the free pointer
  // is actually already pointing to this node, so we don't do anything.
  if (blockPtr != freePtr) {
    // Find the location to insert this block into the free list
    while (true) {
      let nextp = getNext(p)
      if (
        blockPtr > p && blockPtr < nextp ||
        p >= nextp && (blockPtr > p || blockPtr < nextp)
      ) {
        break
      }
      p = nextp
    }

    // Merge the block into the adjacent free list entry above, if needed
    let blockPtrSize = getSize(blockPtr)
    let next = getNext(p)
    if (blockPtr + blockPtrSize == next) {
      setSize(blockPtr, blockPtrSize + getSize(next))
      setNext(blockPtr, getNext(next))
    } else {
      setNext(blockPtr, next)
    }
    // Merge the previous (adjacent) free list entry into this block, if needed
    let pSize = getSize(p)
    if (p + pSize == blockPtr) {
      setSize(p, pSize + getSize(blockPtr))
      setNext(p, getNext(blockPtr))
    } else {
      setNext(p, blockPtr)
    }
    // Set the free list head to this block
    freePtr = p
  }
}

/**
 * Asks the runtime for more heap memory.
 * (if you can't tell from the fact that the name is reminiscient
 *  of the 1970s, the name of this function is taken from K&R).
 *
 * @param nbytes: The number of bytes to try to grow the heap by
 * @returns A pointer to the start of the free list if successful or -1 otherwise
 */
let morecore = (nbytes: WasmI32) => {
  let origSize = heapSize
  let mut cp = growHeap(nbytes)

  // If there was an error, fail
  if (cp == -1n) {
    Exception.panic("OutOfMemory: Maximum memory size exceeded")
  } else {
    // Set the size of the new block to the amount the
    // heap was grown.
    let grownAmount = heapSize - origSize
    setSize(cp, grownAmount)
    // Call free() with the new block to add it to the free list.
    free(cp + 8n)
    // Return the free list pointer.
    freePtr
  }
}

/**
 * Allocates the requested number of bytes, returning a pointer.
 *
 * @param nbytes: The number of bytes to allocate
 * @returns The pointer to the allocated region (8-byte aligned) or -1 if the allocation failed
 */
export let malloc = (nb: WasmI32) => {
  let mut nbytes = nb
  let mut prevp = freePtr

  // Set nbytes to the next multiple of mallocHeaderSize greater
  // than the given size
  let mut nunits = (nbytes + mallocHeaderSize - 1n) / mallocHeaderSize + 1n
  nbytes = nunits << logMallocHeaderSize // multiply by header size

  // Handle initialization
  if (heapSize == 0n) {
    WasmI32.store(_BASE, _BASE, _NEXT_OFFSET)
    freePtr = _BASE
    prevp = _BASE
    WasmI32.store(_BASE, 0n, _SIZE_OFFSET)
  }

  let mut ret = -1n

  // Search the freelist for any blocks large enough.
  for (
    let mut p = getNext(prevp); ; {
      prevp = p
      p = getNext(p)
    }
  ) {
    let size = getSize(p)
    if (size >= nbytes) {
      // If this block is big enough, allocate from it.
      if (size == nbytes) {
        // It's exactly the right size!
        setNext(prevp, getNext(p))
      } else {
        // Shrink it as needed
        let newSize = size - nbytes
        setSize(p, newSize)
        p += newSize
        setSize(p, nbytes)
      }
      // Update the pointer to the free list.
      freePtr = prevp

      // Return region past the header
      ret = p + 8n
      break
    }

    // We've reached the end of the free list. Time to grow the heap.
    if (p == freePtr) {
      // Attempt to grow the heap
      p = morecore(nbytes)
      // If growing the heap failed, return -1.
      if (p == -1n) {
        ret = -1n
        break
      }
    }
  }
  ret
}

/**
 * Returns the current free list pointer.
 * Used for debugging.
 *
 * @returns The free list pointer
 */
export let getFreePtr = () => {
  freePtr
}
