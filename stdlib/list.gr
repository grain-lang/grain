# Standard library for list functionality

export *

let rec length = (list) => {
  match (list) {
    | [] => 0
    | [_, ...rest] => 1 + length(rest)
  }
}

let rec sum = (list) => {
  match (list) {
    | [] => 0
    | [first, ...rest] => first + sum(rest)
  }
}

let reverse = (list) => {
  let rec iter = (list, acc) => {
    match (list) {
      | [] => acc
      | [first, ...rest] => iter(rest, [first, ...acc])
    }
  }
  iter(list, [])
}

let rec append = (list1, list2) => {
  match (list1) {
    | [] => list2
    | [first, ...rest] => [first, ...append(rest, list2)]
  }
}

let rec contains = (value, list) => {
  match (list) {
    | [] => false
    | [first, ...rest] => (first == value) || contains(value, rest)
  }
}

let rec reduce = (fn, acc, list) => {
  match (list) {
    | [] => acc
    | [first, ...rest] => reduce(fn, fn(acc, first), rest)
  }
}

let rec reduceRight = (fn, acc, list) => {
  match (list) {
    | [] => acc
    | [first, ...rest] => fn(first, reduceRight(fn, acc, rest))
  }
}

let rec map = (fn, list) => {
  match (list) {
    | [] => []
    | [first, ...rest] => [fn(first), ...map(fn, rest)]
  }
}

let mapi = (fn, list) => {
  let rec iter = (fn, list, index) => {
    match (list) {
      | [] => []
      | [first, ...rest] => [fn(first, index), ...iter(fn, rest, index + 1)]
    }
  }
  iter(fn, list, 0)
}

let every = (fn, list) => {
  reduce((acc, value) => { acc && fn(value) }, true, list)
}

let some = (fn, list) => {
  reduce((acc, value) => { acc || fn(value) }, false, list)
}

let rec forEach = (fn, list) => {
  match (list) {
    | [] => void
    | [first, ...rest] => {
      fn(first)
      forEach(fn, rest)
    }
  }
}

let forEachi = (fn, list) => {
  let rec iter = (fn, list, index) => {
    match (list) {
      | [] => void
      | [first, ...rest] => {
        fn(first, index)
        iter(fn, rest, index + 1)
      }
    }
  }
  iter(fn, list, 0)
}

let rec filter = (fn, list) => {
  match (list) {
    | [] => []
    | [first, ...rest] => if (fn(first)) [first, ...filter(fn, rest)] else filter(fn, rest)
  }
}

let rec reject = (fn, list) => {
  match (list) {
    | [] => []
    | [first, ...rest] => if (!fn(first)) [first, ...reject(fn, rest)] else reject(fn, rest)
  }
}

let head = (list) => {
  match (list) {
    | [] => fail 'head cannot get the first element of an empty List'
    | [first, ..._] => first
  }
}

let tail = (list) => {
  match (list) {
    | [] => fail 'tail cannot get any elements from an empty List'
    | [_, ...rest] => rest
  }
}

let rec nth = (index, list) => {
  if (index < 0) {
    fail 'nth index cannot be a negative number'
  } else {
    match (list) {
      | [] => fail 'nth index is out-of-bounds'
      | [first, ...rest] => if (index == 0) first else nth(index - 1, rest)
    }
  }
}

let rec flatten = (list) => {
  match (list) {
    | [] => []
    | [first, ...rest] => append(first, flatten(rest))
  }
}

let rec insert = (value, index, list) => {
  if (index < 0) {
    fail 'insert index cannot be a negative number'
  } else {
    match(list) {
      | [] => if (index == 0) [value] else fail 'insert index is out-of-bounds'
      | [first, ...rest] => if (index == 0) [value, ...list] else [first, ...insert(value, index - 1, rest)]
    }
  }
}

let rec count = (fn, list) => {
  match (list) {
    | [] => 0
    | [first, ...rest] => if (fn(first)) 1 + count(fn, rest) else count(fn, rest)
  }
}

let part = (count, list) => {
  if (count < 0) {
    fail 'part count cannot be a negative number'
  } else {
    let rec iter = (list1, list2, count) => {
      match (list2) {
        | [] => if (count > 0) fail 'part count is out-of-bounds' else (list1, list2)
        | [first, ...rest] => if (count > 0) iter([first, ...list1], rest, count - 1) else (list1, list2)
      }
    }
    let (pt1, pt2) = iter([], list, count)
    (reverse(pt1), pt2)
  }
}

let rotate = (count, list) => {
  let (beginning, end) = if (count >= 0) part(count, list) else part(length(list) + count, list)
  append(end, beginning)
}

let unique = (list) => {
  let rec iter = (list, acc) => {
    match (list) {
      | [] => reverse(acc)
      | [first, ...rest] => if (contains(first, acc)) iter(rest, acc) else iter(rest, [first, ...acc])
    }
  }
  iter(list, [])
}
