/**
 * An immutable stack implementation. A stack is a LIFO (last-in-first-out) data structure where new values are added, retrieved, and removed from the end.
 *
 * @example include "stack"
 *
 * @since v0.3.0
 *
 * @deprecated This module will be renamed to ImmutableStack in the v0.6.0 release of Grain.
 */

module Stack

include "list"

/**
 * @section Types: Type declarations included in the Stack module.
 */

/**
 * Stacks are immutable data structures that store their data in a List.
 */
abstract record Stack<a> {
  data: List<a>,
}

/**
 * @section Values: Functions and constants included in the Stack module.
 */

/**
 * An empty stack.
 *
 * @since v0.5.4
 */
provide let empty = {
  let empty = { data: [], }
  empty
}

/**
 * Creates a new stack.
 *
 * @returns An empty stack
 *
 * @since v0.3.0
 * @deprecated This will be removed in the v0.6.0 release of Grain.
 */
provide let make = () => {
  { data: [], }
}

/**
 * Checks if the given stack contains no items.
 *
 * @param stack: The stack to check
 * @returns `true` if the stack has no items or `false` otherwise
 *
 * @since v0.3.0
 */
provide let isEmpty = stack => {
  match (stack) {
    { data: [] } => true,
    _ => false,
  }
}

/**
 * Provides the value at the top of the stack, if it exists.
 *
 * @param stack: The stack to inspect
 * @returns `Some(value)` containing the value at the top of the stack or `None` otherwise.
 *
 * @since v0.3.0
 * @history v0.3.1: Rename from `head` to `peek`
 */
provide let peek = stack => {
  match (stack) {
    { data: [] } => None,
    { data } => List.head(data),
  }
}

/**
 * Adds a new item to the top of the stack.
 *
 * @param value: The item to be added
 * @param stack: The stack being updated
 * @returns A new stack with the item added to the end
 *
 * @since v0.3.0
 */
provide let push = (value, stack) => {
  match (stack) {
    { data: [] } => { data: [value], },
    { data } => { data: [value, ...data], },
  }
}

/**
 * Removes the item at the top of the stack.
 *
 * @param stack: The stack being updated
 * @returns A new stack with the last item removed
 *
 * @since v0.3.0
 */
provide let pop = stack => {
  match (stack) {
    { data: [] } => stack,
    { data: [head, ...tail] } => { data: tail, },
  }
}

/**
 * Computes the size of the input stack.
 *
 * @param stack: The stack to inspect
 * @returns The count of the items in the stack
 *
 * @since v0.3.2
 */
provide let size = stack => {
  match (stack) {
    { data: [] } => 0,
    { data } => List.length(data),
  }
}
