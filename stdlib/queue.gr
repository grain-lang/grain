/**
 * @module Queue: An immutable queue implementation. A queue is a FIFO (first-in-first-out) data structure where new values are added to the end and retrieved or removed from the beginning.
 * @example include "queue"
 * @since v0.2.0
 *
 * @deprecated This module will be renamed to ImmutableQueue in the v0.6.0 release of Grain.
 */

module Queue

include "list"

/**
 * @section Types: Type declarations included in the Queue module.
 */

record Queue<a> {
  forwards: List<a>,
  backwards: List<a>,
}

/**
 * @section Values: Functions and constants for working with queues.
 */

/**
 * An empty queue.
 *
 * @since v0.5.4
 */
provide let empty = {
  let empty = { forwards: [], backwards: [] }
  empty
}

/**
 * Creates an empty queue.
 *
 * @returns An empty queue
 *
 * @deprecated This will be removed in the v0.6.0 release of Grain.
 *
 * @since v0.2.0
 */
provide let make = () => {
  { forwards: [], backwards: [] }
}

/**
 * Checks if the given queue contains any values.
 *
 * @param queue: The queue to check
 * @returns `true` if the given queue is empty or `false` otherwise
 *
 * @since v0.2.0
 */
provide let isEmpty = queue => {
  match (queue) {
    { forwards: [], backwards: [] } => true,
    _ => false,
  }
}

/**
 * Returns the value at the beginning of the queue. It is not removed from the queue.
 *
 * @param queue: The queue to inspect
 * @returns `Some(value)` containing the value at the beginning of the queue, or `None` if the queue is empty
 *
 * @since v0.3.2
 * @history v0.2.0: Originally named `head`
 * @history v0.3.2: Deprecated `head` function
 * @history v0.4.0: Removed `head` function
 */
provide let peek = queue => {
  match (queue) {
    { forwards: [], backwards: [] } => None,
    { forwards, backwards } => List.head(forwards),
  }
}

/**
 * Adds a value to the end of the queue.
 *
 * @param value: The value to append
 * @param queue: The queue to update
 * @returns An updated queue
 *
 * @since v0.3.2
 * @history v0.2.0: Originally named `enqueue`
 * @history v0.3.2: Deprecated `enqueue` function
 * @history v0.4.0: Removed `enqueue` function
 */
provide let push = (value, queue) => {
  match (queue) {
    { forwards: [], backwards: [] } => { forwards: [value], backwards: [] },
    { forwards, backwards } => { forwards, backwards: [value, ...backwards] },
  }
}

/**
 * Dequeues the next value in the queue.
 *
 * @param queue: The queue to change
 * @returns An updated queue
 *
 * @since v0.3.2
 * @history v0.2.0: Originally named `dequeue`
 * @history v0.3.2: Deprecated `dequeue` function
 * @history v0.4.0: Removed `dequeue` function
 */
provide let pop = queue => {
  match (queue) {
    { forwards: [], backwards: [] } => queue,
    { forwards: [head], backwards: [] } => { forwards: [], backwards: [] },
    { forwards: [head], backwards } =>
      {
        forwards: List.reverse(backwards),
        backwards: [],
      },
    { forwards: [head, ...ftail], backwards } => { forwards: ftail, backwards },
  }
}

/**
 * Get the number of values in a queue.
 *
 * @param queue: The queue to inspect
 * @returns The number of values in the queue
 *
 * @since v0.3.2
 */
provide let size = queue => {
  match (queue) {
    { forwards: [], backwards: [] } => 0,
    { forwards, backwards: [] } => List.length(forwards),
    { forwards: [], backwards } => List.length(backwards),
    { forwards, backwards } => List.length(forwards) + List.length(backwards),
  }
}
