import Memory from "runtime/unsafe/memory"
import WasmI32 from "runtime/unsafe/wasmi32"
import WasmI64 from "runtime/unsafe/wasmi64"
import WasmF32 from "runtime/unsafe/wasmf32"
import WasmF64 from "runtime/unsafe/wasmf64"
import Conv from "runtime/unsafe/conv"
import { tagSimpleNumber, allocateBytes, allocateString } from "runtime/dataStructures"
import Exception from "runtime/exception"
import Int32 from "int32"
import { coerceNumberToWasmI32 } from "runtime/numbers"

let _SIZE_OFFSET = 4n;
let _VALUE_OFFSET = 8n;
let _INT8_BYTE_SIZE = 1n;
let _INT16_BYTE_SIZE = 2n;
let _INT32_BYTE_SIZE = 4n;
let _FLOAT32_BYTE_SIZE = 4n;
let _INT64_BYTE_SIZE = 8n;
let _FLOAT64_BYTE_SIZE = 8n;

/** Throws an exception if the index specified is out-of-bounds */
@disableGC
let checkIndexIsInBounds = (i, byteSize, max) => {
  let (+) = WasmI32.add
  let (<) = WasmI32.ltS
  let (>) = WasmI32.gtS
  if (i < 0n) {
    throw Exception.IndexOutOfBounds
  }
  if ((i + byteSize) > max) {
    throw Exception.IndexOutOfBounds
  }
}

/** Gets the size of a Bytes via its ptr */
@disableGC
let getSize = (ptr) => WasmI32.load(ptr, _SIZE_OFFSET)

/**
 * Sets a signed 8-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param v: Int32 - The value to set
 * @param b: Bytes - The byte sequence
 * @returns Void
 */
@disableGC
export let rec setInt8 = (i: Number, v: Int32, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT8_BYTE_SIZE, size)
  let value = Conv.fromInt32(v)
  let ret = WasmI32.store8(ptr + offset, value, _VALUE_OFFSET)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(v))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(setInt8))
  ret
}

/**
 * Gets a signed 8-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param b: Bytes - The byte sequence
 * @returns Int32
 */
@disableGC
export let rec getInt8S = (i: Number, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT8_BYTE_SIZE, size)
  let n = WasmI32.load8S(ptr + offset, _VALUE_OFFSET)
  let ret = Conv.toInt32(n)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(getInt8S))
  ret
}

/**
 * Gets an unsigned 8-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param b: Bytes - The byte sequence
 * @returns Int32
 */
@disableGC
export let rec getInt8U = (i: Number, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT8_BYTE_SIZE, size)
  let n = WasmI32.load8U(ptr + offset, _VALUE_OFFSET)
  let ret = Conv.toInt32(n)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(getInt8U))
  ret
}

/**
 * Sets a signed 16-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param v: Int32 - The value to set
 * @param b: Bytes - The byte sequence
 * @returns Void
 */
@disableGC
export let rec setInt16 = (i: Number, v: Int32, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT16_BYTE_SIZE, size)
  let value = Conv.fromInt32(v)
  let ret = WasmI32.store16(ptr + offset, value, _VALUE_OFFSET)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(v))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(setInt16))
  ret
}

/**
 * Gets a signed 16-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param b: Bytes - The byte sequence
 * @returns Int32
 */
@disableGC
export let rec getInt16S = (i: Number, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT16_BYTE_SIZE, size)
  let n = WasmI32.load16S(ptr + offset, _VALUE_OFFSET)
  let ret = Conv.toInt32(n)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(getInt16S))
  ret
}

/**
 * Gets an unsigned 16-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param b: Bytes - The byte sequence
 * @returns Int32
 */
@disableGC
export let rec getInt16U = (i: Number, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT16_BYTE_SIZE, size)
  let n = WasmI32.load16U(ptr + offset, _VALUE_OFFSET)
  let ret = Conv.toInt32(n)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(getInt16U))
  ret
}

/**
 * Sets a signed 32-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param v: Int32 - The value to set
 * @param b: Bytes - The byte sequence
 * @returns Void
 */
@disableGC
export let rec setInt32 = (i: Number, v: Int32, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT32_BYTE_SIZE, size)
  let value = Conv.fromInt32(v)
  let ret = WasmI32.store(ptr + offset, value, _VALUE_OFFSET)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(v))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(setInt32))
  ret
}

/**
 * Gets a signed 32-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param b: Bytes - The byte sequence
 * @returns Int32
 */
@disableGC
export let rec getInt32 = (i: Number, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT32_BYTE_SIZE, size)
  let n = WasmI32.load(ptr + offset, _VALUE_OFFSET)
  let ret = Conv.toInt32(n)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(getInt32))
  ret
}

/**
 * Sets a 32-bit float starting at the given byte index.
 * @param i: Number - The byte index
 * @param v: Float32 - The value to set
 * @param b: Bytes - The byte sequence
 * @returns Void
 */
@disableGC
export let rec setFloat32 = (i: Number, v: Float32, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT32_BYTE_SIZE, size)
  let value = Conv.fromFloat32(v)
  let ret = WasmF32.store(ptr + offset, value, _VALUE_OFFSET)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(v))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(setFloat32))
  ret
}

/**
 * Gets a 32-bit float starting at the given byte index.
 * @param i: Number - The byte index
 * @param b: Bytes - The byte sequence
 * @returns Float32
 */
@disableGC
export let rec getFloat32 = (i: Number, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT32_BYTE_SIZE, size)
  let n = WasmF32.load(ptr + offset, _VALUE_OFFSET)
  let ret = Conv.toFloat32(n)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(getFloat32))
  ret
}

/**
 * Sets a signed 64-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param v: Int64 - The value to set
 * @param b: Bytes - The byte sequence
 * @returns Void
 */
@disableGC
export let rec setInt64 = (i: Number, v: Int64, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT64_BYTE_SIZE, size)
  let value = Conv.fromInt64(v)
  let ret = WasmI64.store(ptr + offset, value, _VALUE_OFFSET)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(v))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(setInt64))
  ret
}

/**
 * Gets a signed 64-bit integer starting at the given byte index.
 * @param i: Number - The byte index
 * @param b: Bytes - The byte sequence
 * @returns Int64
 */
@disableGC
export let rec getInt64 = (i: Number, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _INT64_BYTE_SIZE, size)
  let n = WasmI64.load(ptr + offset, _VALUE_OFFSET)
  let ret = Conv.toInt64(n)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(getInt64))
  ret
}

/**
 * Sets a 64-bit float starting at the given byte index.
 * @param i: Number - The byte index
 * @param v: Float64 - The value to set
 * @param b: Bytes - The byte sequence
 * @returns Void
 */
@disableGC
export let rec setFloat64 = (i: Number, v: Float64, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _FLOAT64_BYTE_SIZE, size)
  let value = Conv.fromFloat64(v)
  let ret = WasmF64.store(ptr + offset, value, _VALUE_OFFSET)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(v))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(setFloat64))
  ret
}

/**
 * Gets a 64-bit float starting at the given byte index.
 * @param i: Number - The byte index
 * @param b: Bytes - The byte sequence
 * @returns Float64
 */
@disableGC
export let rec getFloat64 = (i: Number, b: Bytes) => {
  let (+) = WasmI32.add
  let ptr = WasmI32.fromGrain(b)
  let size = getSize(ptr)
  let offset = coerceNumberToWasmI32(i)
  checkIndexIsInBounds(offset, _FLOAT64_BYTE_SIZE, size)
  let n = WasmF64.load(ptr + offset, _VALUE_OFFSET)
  let ret = Conv.toFloat64(n)
  Memory.decRef(WasmI32.fromGrain(i))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(getFloat64))
  ret
}

/**
 * Return a new byte sequence that contains the same bytes as the argument.
 * @param b: Bytes - The byte sequence to copy
 * @returns Bytes
 */
@disableGC
export let rec copy = (b: Bytes) => {
  let (+) = WasmI32.add
  let src = WasmI32.fromGrain(b)
  let size = getSize(src)
  let dst = allocateBytes(size)
  Memory.copy(dst + _VALUE_OFFSET, src + _VALUE_OFFSET, size)
  let ret = WasmI32.toGrain(dst): Bytes
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(copy))
  ret
}

/**
 * Returns a new byte sequence containing a subset of the original byte sequence.
 * @param i: Number - The start position to copy from
 * @param len: Number - The number of bytes to copy
 * @param b: Bytes - The byte sequence get a subset of bytes from
 * @returns Bytes
 */
@disableGC
export let rec slice = (i: Number, len: Number, b: Bytes) => {
  let (>) = WasmI32.gtS
  let (+) = WasmI32.add
  let src = WasmI32.fromGrain(b)
  let size = getSize(src)
  let iOrig = i
  let lenOrig = len
  let i = coerceNumberToWasmI32(i)
  let len = coerceNumberToWasmI32(len)
  if ((i + len) > size) {
    throw Exception.InvalidArgument("The given index and length do not specify a valid range of bytes")
  }
  let dst = allocateBytes(len)
  let offset = i
  Memory.copy(dst + _VALUE_OFFSET, src + _VALUE_OFFSET + i, len)
  let ret = WasmI32.toGrain(dst): Bytes
  Memory.decRef(WasmI32.fromGrain(iOrig))
  Memory.decRef(WasmI32.fromGrain(lenOrig))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(slice))
  ret
}


/**
 * Add or remove bytes from the start and/or end of a byte sequence.
 * A positive number represents bytes to add, while a negative number represents bytes to remove.
 * @param left: Number - The number of uninitialized bytes to prepend
 * @param right: Number - The number of uninitialized bytes to append
 * @param b: Bytes - The byte sequence get a subset of bytes from
 * @returns Bytes
 */
@disableGC
export let rec resize = (left: Number, right: Number, b: Bytes) => {
  let (<) = WasmI32.ltS
  let (>) = WasmI32.gtS
  let (+) = WasmI32.add
  let (-) = WasmI32.sub
  let (*) = WasmI32.mul
  let src = WasmI32.fromGrain(b)
  let size = getSize(src)
  let leftOrig = left
  let rightOrig = right
  let left = coerceNumberToWasmI32(left)
  let right = coerceNumberToWasmI32(right)
  let newSize = size + left + right
  if (newSize < 0n) {
    throw Exception.InvalidArgument("The resulting length is less than 0")
  }
  let dst = allocateBytes(newSize)
  let mut srcOffset = 0n;
  let mut dstOffset = 0n;
  if (left < 0n) {
    srcOffset = left * -1n;
    dstOffset = 0n;
  }
  if (left > 0n) {
    srcOffset = 0n;
    dstOffset = left;
  }
  let len = if (right < 0n) {
    (size + right) - srcOffset
  } else {
    size - srcOffset
  }
  if (len > 0n) {
    Memory.copy(dst + _VALUE_OFFSET + dstOffset, src + _VALUE_OFFSET + srcOffset, len)
  }
  let ret = WasmI32.toGrain(dst): Bytes
  Memory.decRef(WasmI32.fromGrain(leftOrig))
  Memory.decRef(WasmI32.fromGrain(rightOrig))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(resize))
  ret
}

/**
 * Copies a range of bytes from a source buffer to a given location in a destination buffer.
 * @param srcPos: Number - The starting byte index to copy bytes from
 * @param dstPos: Number - The starting byte index to copy bytes into
 * @param len: Number - The amount of bytes to copy from the source buffer
 * @param src: Bytes - The source buffer
 * @param dst: Bytes - The destination buffer
 * @returns Void
 */
@disableGC
export let rec move = (srcPos: Number, dstPos: Number, len: Number, src: Bytes, dst: Bytes) => {
  let (>) = WasmI32.gtS
  let (+) = WasmI32.add
  let srcPosOrig = srcPos
  let dstPosOrig = dstPos
  let lenOrig = len
  let src = WasmI32.fromGrain(src)
  let srcSize = getSize(src)
  let srcPos = coerceNumberToWasmI32(srcPos)
  let dst = WasmI32.fromGrain(dst)
  let dstSize = getSize(dst)
  let dstPos = coerceNumberToWasmI32(dstPos)
  let len = coerceNumberToWasmI32(len)
  if ((srcPos + len) > srcSize) {
    throw Exception.InvalidArgument("Invalid source bytes range")
  }
  if ((dstPos + len) > dstSize) {
    throw Exception.InvalidArgument("Invalid destination bytes range")
  }
  let end = srcPos + len
  let ret = Memory.copy(dst + _VALUE_OFFSET + dstPos, src + _VALUE_OFFSET + srcPos, len)
  Memory.decRef(WasmI32.fromGrain(srcPosOrig))
  Memory.decRef(WasmI32.fromGrain(dstPosOrig))
  Memory.decRef(WasmI32.fromGrain(lenOrig))
  Memory.decRef(WasmI32.fromGrain(src))
  Memory.decRef(WasmI32.fromGrain(dst))
  Memory.decRef(WasmI32.fromGrain(move))
  ret
}

/**
 * Get the byte length of a byte sequence.
 * @param b: Bytes - The byte sequence to check
 * @returns Number
 */
 @disableGC
export let rec length = (b: Bytes) => {
  let b = WasmI32.fromGrain(b)
  let ret = tagSimpleNumber(getSize(b))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(length))
  ret
}

/**
 * Creates a new byte sequence that contains the bytes of both buffers a and b.
 * @param a: Bytes - The buffer to be copied first
 * @param b: Bytes - The buffer to be copied last
 * @returns Bytes
 */
export let concat = (a: Bytes, b: Bytes) => {
  let alen = length(a)
  let blen = length(b)
  let c = resize(0, blen, a)
  move(0, alen, blen, b, c)
  c
}

/**
 * Creates a new String from a byte sequence.
 * @param b: Bytes - The source buffer
 * @returns String
 */
@disableGC
export let rec toString = (b: Bytes) => {
  let (+) = WasmI32.add
  let src = WasmI32.fromGrain(b)
  let size = getSize(src)
  let dst = allocateString(size)
  Memory.copy(dst + _VALUE_OFFSET, src + _VALUE_OFFSET, size)
  let ret = WasmI32.toGrain(dst): String
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(toString))
  ret
}

/**
 * Creates a new byte sequence from a String.
 * @param str: String - The String to copy into a byte sequence
 * @returns Bytes
 */
@disableGC
export let rec fromString = (str: String) => {
  let (+) = WasmI32.add
  let src = WasmI32.fromGrain(str)
  let size = getSize(src)
  let dst = allocateBytes(size)
  Memory.copy(dst + _VALUE_OFFSET, src + _VALUE_OFFSET, size)
  let ret = WasmI32.toGrain(dst): Bytes
  Memory.decRef(WasmI32.fromGrain(str))
  Memory.decRef(WasmI32.fromGrain(fromString))
  ret
}

/**
 * Fills a byte sequence with a given value.
 * @param v: Int32 - The value to fill the byte sequence with
 * @param b: Bytes - The byte sequence to fill
 */
@disableGC
export let rec fill = (v: Int32, b: Bytes) => {
  let (+) = WasmI32.add
  let src = WasmI32.fromGrain(b)
  let size = getSize(src)
  let value = Conv.fromInt32(v)
  let ret = Memory.fill(src + _VALUE_OFFSET, value, size)
  Memory.decRef(WasmI32.fromGrain(v))
  Memory.decRef(WasmI32.fromGrain(b))
  Memory.decRef(WasmI32.fromGrain(fill))
  ret
}

/**
 * Make a new byte sequence of n-bytes size.
 * @param n: Number - The number of bytes to store
 * @returns Bytes
 */
@disableGC
export let rec make = (n: Number) => {
  let bytes = allocateBytes(coerceNumberToWasmI32(n))
  let ret = WasmI32.toGrain(bytes): Bytes
  Memory.decRef(WasmI32.fromGrain(n))
  Memory.decRef(WasmI32.fromGrain(make))
  ret
}

/**
 * An empty byte sequence
 */
export let empty = make(0)

