/* grainc-flags --compilation-mode=runtime */
module MallocTight

include "runtime/malloc"
include "runtime/unsafe/wasmi32"
from WasmI32 use { (+), (-), (*), (==), (!=) }

primitive assert = "@assert"
primitive ignore = "@ignore"

primitive heapBase = "@heap.base"

// Because we're in runtime mode, malloc has yet to be called
// This provides us with a clean slate to test malloc

// allow malloc to initialize
ignore(Malloc.malloc(8n))

// The free list should be pointing at the base
let base = Malloc.getFreePtr()
assert base == Malloc._RESERVED_RUNTIME_SPACE + heapBase

// grab the next (and only) block in the free list
let block = WasmI32.load(Malloc.getFreePtr(), 0n)
assert WasmI32.load(block, 0n) == base

// When we allocate, an extra 8 bytes is reserved for the block header
let remainingMemory = WasmI32.load(block, 4n) - 8n

let firstPtr = Malloc.malloc(remainingMemory)
let secondPtr = Malloc.malloc(remainingMemory)

// These two pointers should (obviously) be different
assert firstPtr != secondPtr
