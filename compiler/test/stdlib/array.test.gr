module ArrayTest

from "list" include List
from "array" include Array
from "string" include String
from "range" include Range

let arr = [> 1, 2, 3]

// Array.get

assert Array.get(1, arr) == 2
assert Array.get(-1, arr) == 3
assert Array.get(-2, arr) == 2
assert Array.get(0, arr) == 1

// Array.set

let arr = [> 1, 2]

Array.set(0, 5, arr)
assert arr[0] == 5
Array.set(-1, 5, arr)
assert arr[1] == 5

// Array.append

let arr1 = [> 1, 2]
let arr2 = [> 3, 4, 5]

assert Array.append(arr1, arr2) == [> 1, 2, 3, 4, 5]
assert Array.append([>], [>]) == [>]
assert Array.append(arr1, [>]) == arr1
assert Array.append([>], arr1) == arr1

// Array.concat

let arr1 = [> 1, 2]
let arr2 = [>]
let arr3 = [> 3, 4, 5]
let arr4 = [> 6, 7, 8, 9]

assert Array.concat([]) == [>]
assert Array.concat([[>]]) == [>]
assert Array.concat([[>], [>]]) == [>]
assert Array.concat([arr3]) == [> 3, 4, 5]
assert Array.concat([arr1, arr2, arr3, arr4]) == [> 1, 2, 3, 4, 5, 6, 7, 8, 9]

// Array.copy

let arr1 = [>]
let arr2 = [> 1, 2]

assert Array.copy(arr1) == arr1
assert !(Array.copy(arr1) is arr1)

assert Array.copy(arr2) == arr2
assert !(Array.copy(arr2) is arr2)

// Array.cycle

let arr1 = [> 1, 2, 3]
let mut sum = 0

let sentinel = _n => {
  sum = 42
}

Array.cycle(sentinel, 3, [>])

assert sum == 0

let addToSum = n => {
  sum += n
}

Array.cycle(addToSum, 2, arr1)

assert sum == 12

let arr2 = [> "a", "b", "c"]
let mut word = ""
let addToWord = n => {
  word = word ++ n
}

Array.cycle(addToWord, 2, arr2)

assert word == "abcabc"

sum = 0

Array.cycle(sentinel, 0, arr1)

assert sum == 0

Array.cycle(sentinel, -42, arr1)

assert sum == 0
// Array.forEach

let arr1 = [> 1, 2, 3]
let mut sum = 0

let sentinel = _n => {
  sum = 42
}

Array.forEach(sentinel, [>])

assert sum == 0

let addToSum = n => {
  sum += n
}

Array.forEach(addToSum, arr1)

assert sum == 6

// Array.forEachi

let arr1 = [> 1, 2, 3]
let mut sum = 0

let sentinel = (_n, _i) => {
  sum = 42
}

Array.forEachi(sentinel, [>])

assert sum == 0

let addToSum = (n, i) => {
  sum += n * i
}

Array.forEachi(addToSum, arr1)

assert sum == 8

// Array.reverse

assert Array.reverse([>]) == [>] // Empty
let testArr = [> 1, 2, 3, 4]
assert Array.reverse(testArr) == [> 4, 3, 2, 1] // array is reversed properly
assert testArr == [> 1, 2, 3, 4] // original array is not modified

// Array.toList

assert Array.toList([>]) == []
assert Array.toList([> 1, 2, 3]) == [1, 2, 3]

// Array.fromList

assert Array.fromList([]) == [>]
assert Array.fromList([1, 2, 3]) == [> 1, 2, 3]

// Array.map

assert Array.map(incr, [>]) == [>]
assert Array.map(incr, [> 2, 3, 4]) == [> 3, 4, 5]

// Array.mapi

let numbers = [> 2, 3, 4]
let square = (n, i) => n * numbers[i]

assert Array.mapi(square, [>]) == [>]
assert Array.mapi(square, numbers) == [> 4, 9, 16]

// Array.contains

let arr = [> 1, 2, 3]

assert Array.contains(3, arr)
assert !Array.contains(4, arr)

// Array.find

assert Array.find(x => x == 2, arr) == Some(2)
assert Array.find(x => fail "Shoulnd't be called", [>]) == None
assert Array.find(x => false, arr) == None

// Array.findIndex

assert Array.findIndex(x => x == 1, arr) == Some(0)
assert Array.findIndex(x => x == 2, arr) == Some(1)
assert Array.findIndex(x => fail "Shouldn't be called", [>]) == None
assert Array.findIndex(x => false, arr) == None

// Array.fill

let arr = [>]
assert Array.fill(1, arr) == void
assert arr == [>]

let arr = [> 1, 2, 3]
Array.fill(4, arr)
assert arr == [> 4, 4, 4]

// Array.fillRange

let arr = [>]
assert Array.fillRange(0, 0, 0, arr) == void
assert arr == [>]

let arr = [> 1, 2, 3]
Array.fillRange(0, 0, 2, arr)
assert arr == [> 0, 0, 3]

let arr = [> 1, 2, 3, 4, 5]
Array.fillRange(10, 0, 6, arr)
assert arr == [> 10, 10, 10, 10, 10]

let arr = [> 1, 2, 3, 4, 5]
Array.fillRange(10, -4, -2, arr)
assert arr == [> 1, 10, 10, 4, 5]

let arr = [> 1, 2, 3]
Array.fillRange(10, -3, -1, arr)
assert arr == [> 10, 10, 3]

let arr = [> 1, 2, 3]
Array.fillRange(10, -3, -10, arr)
assert arr == [> 10, 10, 10]

// Array.product

let arrA = [> 1, 2]
let arrB = [> 2, 3, 4]
let arrC = [> 1]
let arrD = [> 2]

assert Array.product(arrA, arrB) ==
  [> (1, 2), (1, 3), (1, 4), (2, 2), (2, 3), (2, 4)]
assert Array.product(arrC, arrB) == [> (1, 2), (1, 3), (1, 4)]
assert Array.product(arrC, arrD) == [> (1, 2)]
assert Array.product([>], [>]) == [>]

// Array.zip

let arrA = [> 1, 2, 3, 4, 5]
let arrB = [> 5, 4, 3, 2, 1]

assert Array.zip(arrA, arrB) == [> (1, 5), (2, 4), (3, 3), (4, 2), (5, 1)]

let arrA = [> 1, 2, 3, 4, 5]
let arrB = [> 5, 4, 3, 2, 1, 6, 7]

assert Array.zip(arrA, arrB) == [> (1, 5), (2, 4), (3, 3), (4, 2), (5, 1)]

let arrA = [> 1, 2, 3, 4, 5, 6, 7]
let arrB = [> 5, 4, 3, 2, 1]

assert Array.zip(arrA, arrB) == [> (1, 5), (2, 4), (3, 3), (4, 2), (5, 1)]

// Array.zipWith

let addFn = (a, b) => a + b
let arrA = [> 1, 2, 3]
let arrB = [> 4, 5, 6]

assert Array.zipWith(addFn, arrA, arrB) == [> 5, 7, 9]

let multFn = (a, b) => a * b
let arrB = [> 4, 5]
assert Array.zipWith(multFn, arrA, arrB) == [> 4, 10]
assert Array.zipWith(addFn, [>], arrB) == [>]

// Array.unzip

let arrA = [> 1, 2, 3, 4, 5]
let arrB = [> 5, 4, 3, 2, 1]

assert Array.unzip(Array.zip(arrA, arrB)) == (arrA, arrB)
assert Array.unzip([>]) == ([>], [>])

// Array.count

let arr = [> 1, 2, 3]

assert Array.count(x => x > 0, arr) == 3
assert Array.count(x => x > 0, [>]) == 0
assert Array.count(x => x == 3, arr) == 1

// Array.counti

assert Array.counti((x, i) => x - 1 == i, arr) == 3
assert Array.counti((x, i) => x == i, [>]) == 0
assert Array.counti((x, i) => i == 2, arr) == 1

// TODO: test with wrong indexes and assert the exception.

// Array.reduce

assert Array.reduce((acc, x) => acc + x, 0, arr) == 6
assert Array.reduce((acc, x) => fail "Shouldn't be called", 0, [>]) == 0

// Array.reducei

assert Array.reducei((acc, x, i) => acc + x + i, 0, arr) == 9
assert Array.reducei((acc, x, i) => fail "Shouldn't be called", 0, [>]) == 0

// Array.reduceRight

assert Array.reduceRight((acc, cur) => acc - cur, 0, arr) == 2
assert Array.reduceRight((acc, x) => fail "Shouldn't be called", 0, [>]) == 0

// Array.flatMap

assert Array.flatMap(x => [> x, x * 2], arr) == [> 1, 2, 2, 4, 3, 6]
assert Array.flatMap(x => fail "Shouldn't be called", [>]) == [>]

// Array.every

assert Array.every(x => x < 4, arr) == true
assert Array.every(x => x > 4, arr) == false
assert Array.every(x => fail "Shouldn't be called", [>])

// Array.some

assert Array.some(x => x > 1, arr) == true
assert Array.some(x => x > 4, arr) == false
assert !Array.some(x => fail "Shouldn't be called", [>])

// Array.filter

assert Array.filter(x => true, arr) == [> 1, 2, 3]
assert Array.filter(x => x > 1, arr) == [> 2, 3]
assert Array.filter(x => x % 2 == 0, arr) == [> 2]
assert Array.filter(x => fail "Shouldn't be called", [>]) == [>]

// Array.filteri

assert Array.filteri((x, i) => x != i, [> 0, 1, 2, 3, 4]) == [>]
assert Array.filteri((x, i) => x == i, [> 4, 3, 2, 1, 0]) == [> 2]
assert Array.filteri((x, i) => fail "Shouldn't be called", [>]) == [>]

// Array.unique

assert Array.unique([> 1, 1, 2, 2, 3, 3]) == [> 1, 2, 3]
assert Array.unique([> 0, 1, 2]) == [> 0, 1, 2]
assert Array.unique([> 0, 0, 0, 0, 0, 0]) == [> 0]
assert Array.unique([>]) == [>]

// Array.join
assert Array.join(", ", [> "a", "b", "c"]) == "a, b, c"
assert Array.join(", ", [>]) == ""

// Array.slice
let testChars = [> 'a', 'b', 'c']

assert Array.slice(0, end=1, testChars) == [> 'a']
assert Array.slice(1, end=Array.length(testChars), testChars) == [> 'b', 'c']
assert Array.slice(1, testChars) == [> 'b', 'c']
assert Array.slice(0, end=0, testChars) == [>]
// Purposefully huge number
assert Array.slice(1, end=10000, testChars) == [> 'b', 'c']
// Negative indexing
assert Array.slice(1, end=-1, testChars) == [> 'b']
assert Array.slice(-2, end=-1, testChars) == [> 'b']
// Bad order
assert Array.slice(2, end=1, testChars) == [>]
assert Array.slice(-1, end=-2, testChars) == [>]
// Empty
assert Array.slice(1, end=4, [>]) == [>]

// Array.sort
// Numbers
let arr = [> 5, 3, 4, 6, 2, 1]
Array.sort(arr)
assert arr == [> 1, 2, 3, 4, 5, 6]
// Empty
let arr = [>]
Array.sort(arr)
assert arr == [>]
// Strings
let compareLengths = (left, right) => {
  match ((String.length(left), String.length(right))) {
    (left, right) when left > right => 1,
    (left, right) when left == right => 0,
    _ => -1,
  }
}
let arr = [> "a", "abcde", "abc", "ab", "abcd", "a"]
let result = Array.sort(compare=compareLengths, arr)
assert arr == [> "a", "a", "ab", "abc", "abcd", "abcde"]
// Returns void
assert result == void

// Array.rotate
let arr1 = [> 1, 2, 3, 4, 5]
Array.rotate(0, arr1)
assert arr1 == [> 1, 2, 3, 4, 5]

let arr2 = [> 1, 2, 3, 4, 5]
Array.rotate(3, arr2)
assert arr2 == [> 4, 5, 1, 2, 3]

let arr3 = [> 1, 2, 3, 4, 5]
Array.rotate(-4, arr3)
assert arr3 == [> 2, 3, 4, 5, 1]

let arr4 = [> 1, 2, 3, 4, 5]
Array.rotate(5, arr4)
assert arr4 == [> 1, 2, 3, 4, 5]

let arr5 = [> 1, 2, 3, 4, 5]
Array.rotate(48, arr5)
assert arr5 == [> 4, 5, 1, 2, 3]

let arr6 = [> 1, 2, 3, 4, 5]
Array.rotate(-54, arr6)
assert arr6 == [> 2, 3, 4, 5, 1]

let arr7 = [>]: Array<Number>
Array.rotate(1, arr7)
assert arr7 == [>]

let arr8 = [> "a", "b", "c"]
Array.rotate(1, arr8)
assert arr8 == [> "b", "c", "a"]

let arr9 = [>]
Array.rotate(1, arr9)
assert arr9 == [>]

// Array.chunk
assert Array.chunk(2, [>]) == [>]
assert Array.chunk(2, [> 1, 2, 3, 4]) == [> [> 1, 2], [> 3, 4]]
assert Array.chunk(2, [> 1, 2, 3, 4, 5]) == [> [> 1, 2], [> 3, 4], [> 5]]
assert Array.chunk(3, [> 1, 2, 3, 4, 5, 6, 7, 8]) ==
  [> [> 1, 2, 3], [> 4, 5, 6], [> 7, 8]]
assert Array.chunk(3, [> 1, 2, 3, 4, 5, 6, 7, 8]) ==
  [> [> 1, 2, 3], [> 4, 5, 6], [> 7, 8]]
assert Array.chunk(10, [> 1, 2, 3, 4, 5]) == [> [> 1, 2, 3, 4, 5]]

module Immutable {
  use Array.{ module Immutable as Array }

  let fromList = Array.fromList
  let arr = fromList([1, 2, 3])

  // for testing the case where the tail is empty
  let noTail = Range.map(identity, { rangeStart: 0, rangeEnd: 32 })
  let noTailArr = fromList(noTail)

  // for testing the case where the tree branches and has a tail of max length
  let branchingLongTailLen = 32 * 33 + 31
  let branchingLongTail = Range.map(
    identity,
    { rangeStart: 0, rangeEnd: branchingLongTailLen }
  )
  let bltArr = fromList(branchingLongTail)

  // for testing the case where the tree is about to branch
  let almostBranchingLen = 32 * 32 + 31
  let almostBranching = Range.map(
    identity,
    { rangeStart: 0, rangeEnd: almostBranchingLen }
  )
  let abArr = fromList(almostBranching)

  assert Array.empty == fromList([])

  // Array.length & Array.isEmpty

  assert Array.length(Array.empty) == 0
  assert Array.isEmpty(Array.empty)
  assert Array.length(fromList([1, 2, 3])) == 3
  assert !Array.isEmpty(fromList([1, 2, 3]))
  assert Array.length(bltArr) == branchingLongTailLen
  assert Array.length(noTailArr) == 32

  // Array.get

  assert Array.get(1, arr) == 2
  assert Array.get(-1, arr) == 3
  assert Array.get(-2, arr) == 2
  assert Array.get(0, arr) == 1

  // Array.set

  let arr = fromList([1, 2])

  let arr = Array.set(0, 5, arr)
  assert Array.get(0, arr) == 5
  let arr = Array.set(-1, 5, arr)
  assert Array.get(1, arr) == 5
  let arr = Array.set(32, -1, bltArr)
  assert Array.get(32, arr) == -1
  let arr = Array.set(branchingLongTailLen - 1, -1, bltArr)
  assert Array.get(branchingLongTailLen - 1, arr) == -1

  // Array.init

  assert Array.init(5, identity) == fromList([0, 1, 2, 3, 4])
  assert Array.init(10, x => x * 2) ==
    fromList([0, 2, 4, 6, 8, 10, 12, 14, 16, 18])
  assert Array.init(0, x => fail "called init on 0 length") == fromList([])
  assert Array.init(branchingLongTailLen, identity) ==
    fromList(branchingLongTail)
  assert Array.init(32, identity) == fromList(noTail)

  // Array.make

  assert Array.make(5, 10) == fromList([10, 10, 10, 10, 10])
  assert Array.make(0, 10) == fromList([])
  assert Array.make(6, "a") == fromList(["a", "a", "a", "a", "a", "a"])

  // Array.append

  let arr1 = fromList([1, 2])
  let arr2 = fromList([3, 4, 5])

  assert Array.append(arr1, arr2) == fromList([1, 2, 3, 4, 5])
  assert Array.append(fromList([]), fromList([])) == fromList([])
  assert Array.append(arr1, fromList([])) == arr1
  assert Array.append(fromList([]), arr1) == arr1
  assert Array.append(abArr, fromList([almostBranchingLen])) ==
    fromList(List.append(almostBranching, [almostBranchingLen]))
  let val = branchingLongTailLen
  assert Array.append(bltArr, fromList([val, val + 1, val + 2])) ==
    fromList(List.append(branchingLongTail, [val, val + 1, val + 2]))
  assert Array.append(fromList([-3, -2, -1]), bltArr) ==
    fromList([-3, -2, -1, ...branchingLongTail])
  let vals = Range.map(x => x + 32, { rangeStart: 0, rangeEnd: 128 })
  assert Array.append(noTailArr, fromList(vals)) ==
    fromList(List.append(noTail, vals))
  let vals = Range.map(x => x + 32, { rangeStart: 0, rangeEnd: 128 + 31 })
  assert Array.append(noTailArr, fromList(vals)) ==
    fromList(List.append(noTail, vals))
  assert Array.append(noTailArr, noTailArr) ==
    fromList(List.append(noTail, noTail))

  // Array.concat

  let arr1 = fromList([1, 2])
  let arr2 = fromList([])
  let arr3 = fromList([3, 4, 5])
  let arr4 = fromList([6, 7, 8, 9])

  assert Array.concat([]) == fromList([])
  assert Array.concat([fromList([])]) == fromList([])
  assert Array.concat([fromList([]), fromList([])]) == fromList([])
  assert Array.concat([arr3]) == fromList([3, 4, 5])
  assert Array.concat([arr1, arr2, arr3, arr4]) ==
    fromList([1, 2, 3, 4, 5, 6, 7, 8, 9])

  // Array.cycle

  let arr1 = fromList([1, 2, 3])
  let mut sum = 0

  let sentinel = _n => {
    sum = 42
  }

  Array.cycle(sentinel, 3, fromList([]))

  assert sum == 0

  let addToSum = n => {
    sum += n
  }

  Array.cycle(addToSum, 2, arr1)

  assert sum == 12

  let arr2 = fromList(["a", "b", "c"])
  let mut word = ""
  let addToWord = n => {
    word = word ++ n
  }

  Array.cycle(addToWord, 2, arr2)

  assert word == "abcabc"

  sum = 0

  Array.cycle(sentinel, 0, arr1)

  assert sum == 0

  Array.cycle(sentinel, -42, arr1)

  assert sum == 0

  // Array.forEach

  let arr1 = fromList([1, 2, 3])
  let mut sum = 0

  let sentinel = _n => {
    sum = 42
  }

  Array.forEach(sentinel, fromList([]))

  assert sum == 0

  let addToSum = n => {
    sum += n
  }

  Array.forEach(addToSum, arr1)

  assert sum == 6

  let mut list = []
  Array.forEach(x => list = [x, ...list], bltArr)
  assert List.reverse(list) == branchingLongTail

  // Array.reverse

  assert Array.reverse(fromList([])) == fromList([]) // Empty
  let testArr = fromList([1, 2, 3, 4])
  assert Array.reverse(testArr) == fromList([4, 3, 2, 1]) // array is reversed properly
  assert testArr == fromList([1, 2, 3, 4]) // original array is not modified

  // Array.toList

  assert Array.toList(fromList([])) == []
  assert Array.toList(fromList([1, 2, 3])) == [1, 2, 3]
  assert Array.toList(fromList(branchingLongTail)) == branchingLongTail
  assert Array.toList(fromList(noTail)) == noTail

  // Array.map

  assert Array.map(
    x => fail "Map iterator should not be called on 0-length array",
    fromList([])
  ) ==
    fromList([])
  assert Array.map(incr, fromList([2, 3, 4])) == fromList([3, 4, 5])
  assert Array.map(incr, bltArr) == fromList(List.map(incr, branchingLongTail))

  // Array.contains

  let arr = fromList([1, 2, 3])

  assert Array.contains(3, arr)
  assert !Array.contains(4, arr)

  // Array.find

  assert Array.find(x => x == 2, arr) == Some(2)
  assert Array.find(x => fail "Shoulnd't be called", fromList([])) == None
  assert Array.find(x => false, arr) == None

  // Array.findIndex

  assert Array.findIndex(x => x == 1, arr) == Some(0)
  assert Array.findIndex(x => x == 2, arr) == Some(1)
  assert Array.findIndex(x => fail "Shouldn't be called", fromList([])) == None
  assert Array.findIndex(x => false, arr) == None

  // Array.product

  let arrA = fromList([1, 2])
  let arrB = fromList([2, 3, 4])
  let arrC = fromList([1])
  let arrD = fromList([2])

  assert Array.product(arrA, arrB) ==
    fromList([(1, 2), (1, 3), (1, 4), (2, 2), (2, 3), (2, 4)])
  assert Array.product(arrC, arrB) == fromList([(1, 2), (1, 3), (1, 4)])
  assert Array.product(arrC, arrD) == fromList([(1, 2)])
  assert Array.product(fromList([]), fromList([])) == fromList([])

  // Array.zip

  let arrA = fromList([1, 2, 3, 4, 5])
  let arrB = fromList([5, 4, 3, 2, 1])

  assert Array.zip(arrA, arrB) ==
    fromList([(1, 5), (2, 4), (3, 3), (4, 2), (5, 1)])

  let arrA = fromList([1, 2, 3])
  let arrB = fromList([5, 4, 3, 2, 1])

  assert Array.zip(arrA, arrB) == fromList([(1, 5), (2, 4), (3, 3)])
  assert Array.zip(arrB, arrA) == fromList([(5, 1), (4, 2), (3, 3)])

  // Array.zipWith

  let addFn = (a, b) => a + b
  let arrA = fromList([1, 2, 3])
  let arrB = fromList([4, 5, 6])

  assert Array.zipWith(addFn, arrA, arrB) == fromList([5, 7, 9])

  let arrB = fromList([4, 5, 6, 7, 8])

  assert Array.zipWith(addFn, arrA, arrB) == fromList([5, 7, 9])
  assert Array.zipWith(addFn, arrB, arrA) == fromList([5, 7, 9])

  let multFn = (a, b) => a * b
  let arrB = fromList([4, 5])
  assert Array.zipWith(multFn, arrA, arrB) == fromList([4, 10])
  assert Array.zipWith(addFn, fromList([]), arrB) == fromList([])

  // Array.unzip

  let arrA = fromList([1, 2, 3, 4, 5])
  let arrB = fromList([5, 4, 3, 2, 1])

  assert Array.unzip(Array.zip(arrA, arrB)) == (arrA, arrB)

  let arrA = fromList([1, 2, 3, 4])
  let arrB = fromList([3, 2, 1])
  let trimmedA = fromList([1, 2, 3])
  assert Array.unzip(Array.zip(arrA, arrB)) == (trimmedA, arrB)
  assert Array.unzip(Array.zip(arrB, arrA)) == (arrB, trimmedA)

  assert Array.unzip(fromList([])) == (fromList([]), fromList([]))

  // Array.count

  let arr = fromList([1, 2, 3])

  assert Array.count(x => x > 0, arr) == 3
  assert Array.count(
    x => fail "count iterator should not be called on empty array",
    fromList([])
  ) ==
    0
  assert Array.count(x => x == 3, arr) == 1

  // Array.reduce

  assert Array.reduce((acc, x) => acc + x, 0, arr) == 6
  assert Array.reduce((acc, x) => fail "Shouldn't be called", 0, fromList([])) ==
    0
  assert Array.reduce((+), 0, bltArr) == List.reduce((+), 0, branchingLongTail)

  // Array.reduceRight

  assert Array.reduceRight((cur, acc) => cur - acc, 0, arr) == 2
  assert Array.reduceRight(
    (x, acc) => fail "Shouldn't be called",
    0,
    fromList([])
  ) ==
    0
  assert Array.reduceRight((x, acc) => [x, ...acc], [], bltArr) ==
    branchingLongTail

  // Array.flatMap

  assert Array.flatMap(x => fromList([x, x * 2]), arr) ==
    fromList([1, 2, 2, 4, 3, 6])
  assert Array.flatMap(x => fail "Shouldn't be called", fromList([])) ==
    fromList([])

  // Array.every

  assert Array.every(x => x < 4, arr) == true
  assert Array.every(x => x > 4, arr) == false
  assert Array.every(x => fail "Shouldn't be called", fromList([]))

  // Array.some

  assert Array.some(x => x > 1, arr) == true
  assert Array.some(x => x > 4, arr) == false
  assert !Array.some(x => fail "Shouldn't be called", fromList([]))

  // Array.filter

  assert Array.filter(x => true, arr) == fromList([1, 2, 3])
  assert Array.filter(x => x > 1, arr) == fromList([2, 3])
  assert Array.filter(x => x % 2 == 0, arr) == fromList([2])
  assert Array.filter(x => fail "Shouldn't be called", fromList([])) ==
    fromList([])

  // Array.unique

  assert Array.unique(fromList([1, 1, 2, 2, 3, 3])) == fromList([1, 2, 3])
  assert Array.unique(fromList([0, 1, 2])) == fromList([0, 1, 2])
  assert Array.unique(fromList([0, 0, 0, 0, 0, 0])) == fromList([0])
  assert Array.unique(fromList([])) == fromList([])

  // Array.join
  assert Array.join(", ", fromList(["a", "b", "c"])) == "a, b, c"
  assert Array.join(", ", fromList(["some text"])) == "some text"
  assert Array.join(
    " - ",
    fromList(["some text", "some more text", "even more text"])
  ) ==
    "some text - some more text - even more text"
  assert Array.join(", ", fromList([])) == ""

  // Array.slice
  let testChars = fromList(['a', 'b', 'c'])

  assert Array.slice(0, end=1, testChars) == fromList(['a'])
  assert Array.slice(1, end=Array.length(testChars), testChars) ==
    fromList(['b', 'c'])
  assert Array.slice(1, testChars) == fromList(['b', 'c'])
  assert Array.slice(0, end=0, testChars) == fromList([])
  // Purposefully huge number
  assert Array.slice(1, end=10000, testChars) == fromList(['b', 'c'])
  // Negative indexing
  assert Array.slice(1, end=-1, testChars) == fromList(['b'])
  assert Array.slice(-2, end=-1, testChars) == fromList(['b'])
  // Bad order
  assert Array.slice(2, end=1, testChars) == fromList([])
  assert Array.slice(-1, end=-2, testChars) == fromList([])
  // Empty
  assert Array.slice(1, end=4, fromList([])) == fromList([])

  // Array.sort
  // Numbers
  let arr = fromList([5, 3, 4, 6, 2, 1])
  let result = Array.sort(compare=compare, arr)
  assert result == fromList([1, 2, 3, 4, 5, 6])
  // Empty
  let arr = fromList([])
  let result = Array.sort(arr)
  assert result == fromList([])
  // Strings
  let compareLengths = (left, right) => {
    match ((String.length(left), String.length(right))) {
      (left, right) when left > right => 1,
      (left, right) when left == right => 0,
      _ => -1,
    }
  }
  let arr = fromList(["a", "abcde", "abc", "ab", "abcd", "a"])
  let result = Array.sort(compare=compareLengths, arr)
  assert result == fromList(["a", "a", "ab", "abc", "abcd", "abcde"])

  // Array.rotate
  let arr1 = fromList([1, 2, 3, 4, 5])
  Array.rotate(0, arr1)
  assert arr1 == fromList([1, 2, 3, 4, 5])

  let arr2 = fromList([1, 2, 3, 4, 5])
  let result = Array.rotate(3, arr2)
  assert result == fromList([4, 5, 1, 2, 3])

  let arr3 = fromList([1, 2, 3, 4, 5])
  let result = Array.rotate(-4, arr3)
  assert result == fromList([2, 3, 4, 5, 1])

  let arr4 = fromList([1, 2, 3, 4, 5])
  let result = Array.rotate(5, arr4)
  assert result == fromList([1, 2, 3, 4, 5])

  let arr5 = fromList([1, 2, 3, 4, 5])
  let result = Array.rotate(48, arr5)
  assert result == fromList([4, 5, 1, 2, 3])

  let arr6 = fromList([1, 2, 3, 4, 5])
  let result = Array.rotate(-54, arr6)
  assert result == fromList([2, 3, 4, 5, 1])

  let arr7 = fromList([])
  let result = Array.rotate(1, arr7)
  assert result == fromList([])

  let arr8 = fromList(["a", "b", "c"])
  let result = Array.rotate(1, arr8)
  assert result == fromList(["b", "c", "a"])
}
